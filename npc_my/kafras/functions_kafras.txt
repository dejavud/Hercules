//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2024 Hercules Dev Team
//= Copyright (C) Dastgir
//= Copyright (C) Haru
//= Copyright (C) rAthena Dev Team
//= Copyright (C) Euphy
//= Copyright (C) Joseph
//= Copyright (C) eAthena Dev Team
//= Copyright (C) Daegaladh
//= Copyright (C) brianluau
//= Copyright (C) Kisuka
//= Copyright (C) Evera
//= Copyright (C) erKURITA
//= Copyright (C) Silentdragon
//= Copyright (C) Nexon
//= Copyright (C) L0ne_W0lf
//= Copyright (C) kobra_k88
//= Copyright (C) Lupu
//= Copyright (C) Syrus22
//= Copyright (C) Darkchild
//= Copyright (C) Darlskies
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Kafra Functions
//================= Description ===========================================
//= These functions handle save, storage, cart rental, teleport, and Free
//= Teleport/Warp/Rent Cart options for all Kafra NPCs.
//================= Current Version =======================================
//= 6.9
//=========================================================================

//== Main Function =========================================
// Arguments:
// - 0: Used to determine which welcome message to show.
// - 1: Used to determine which menu to display.
// - 2: Used to determine if the info menu is shown in F_KafInfo.
// - 3: Cost to use Storage
// - 4: Cost to Rent a Pushcart
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; // Clear outdated, unused variables
	// Display Kafra Welcome Message
	switch (getarg(0)) {
		default:
		case 0:
			// Default message (obsolete)
			mes "[卡普拉服务人员]";
			mes "欢迎来到卡普拉服务中心.";
			mes "卡普拉的服务永远在您身边.";
			mes "有什么可以帮助您的吗?";
			break;
		case 1:
			// Niflheim specific message
			mes "[卡普拉服务人员]";
			mes "^666666W-weeeelc-c-come";
			mes "to th-the K-kaaafrrrra";
			mes "C-coorpoor-r-ratioooonn...^000000";
			break;
		case 2:
			// Guild Castle Kafra message
			@GID = getcharid(CHAR_ID_GUILD);
			mes "[卡普拉服务人员]";
			mes "欢迎光临. ^ff0000" + getguildinfo(GUILDINFO_NAME, @GID) + "^000000 成员.";
			mes "卡普拉公司永远为您服务.";
			break;
		case 3:
			// Amatsu specific message (obsolete)
			mes "[卡普拉服务人员]";
			mes "所以，您来自一个遥远的土地来研究我们的文化，或者您只是观光?";
			mes "在这种情况下, 为什么不停留片刻?";
			mes "空气中充满了永恒的沉重与愉快的野花的香味.";
			break;
		case 4:
			// Louyang and Ayothaya specific message (obsolete)
			mes "[卡普拉服务人员]";
			mes "有我们许多的卡普拉服务人员的地方";
			mes "让您体验宾至如归.";
			break;
		case 5:
			// NPC has it's own welcome message. (Display nothing)
			break;
	}
	next;
	while (true) {
		deletearray @K_Menu0$;
		if (getarg(0) == 2) {
			// Guild Kafra Menu override (free Teleport, free Storage, Free Cart)
			setarray @K_Menu0$[0],"仓库服务","公会仓库服务","手推车租赁服务","传送服务","取消";
		} else {
			switch (getarg(1)) {
				// Save and Storage only
				case 1:	setarray @K_Menu0$[0],"储存服务","仓库服务","取消"; break;
				// Storage only
				case 2:	setarray @K_Menu0$[0],"仓库服务","取消"; break;
				// No Teleport (Common)
				case 3: setarray @K_Menu0$[0],"储存服务","仓库服务","手推车租赁服务","查询其它信息","取消"; break;
				// Case 4 is Einbroch no tele message.
				// No save, or teleport. (Common)
				case 5:	setarray @K_Menu0$[0],"仓库服务","手推车租赁服务","查询其它信息","取消"; break;
				// Storage and Check Other Information only.
				case 6:	setarray @K_Menu0$[0],"仓库服务","查询其它信息","取消"; break;
				// Save, Storage, and Pushcart only (Kafra Warehouse)
				case 7:	setarray @K_Menu0$[0],"储存服务","仓库服务","手推车租赁服务","取消"; break;
				// Save, Storage, Other Check information. (Turbo track)
				case 8: setarray @K_Menu0$[0],"储存服务","仓库服务","查询其它信息","取消"; break;
				// No Save (Rune Knight)
				case 9: setarray @K_Menu0$[0],"仓库服务","手推车租赁服务","传送服务","查询其它信息","取消"; break;
				// Storage, Save, and Pushcart (Dewata, reorder of case 7)
				case 10: setarray @K_Menu0$[0],"仓库服务","储存服务","手推车租赁服务","取消"; break;
				// Default message (obsolete)
				default: setarray @K_Menu0$[0],"储存服务","仓库服务","传送服务","手推车租赁服务","查询其它信息","取消"; break;
			}
		}
		.@menu$ = "";
		for (.@i = 0; .@i < getarraysize(@K_Menu0$); ++.@i)
			.@menu$ += @K_Menu0$[.@i] + ":";
		.@j = select(.@menu$) - 1;
		if (@K_Menu0$[.@j] == "储存服务") {
			return;
		} else if (@K_Menu0$[.@j] == "仓库服务") {
			// Do not charge for Guild Storage
			if(getarg(0) == 2)
				callfunc "F_KafStor",2,0,0,"[卡普拉服务人员]";
			else
				callfunc "F_KafStor",0,getarg(3),getarg(0),"[卡普拉服务人员]";
			next;
		} else if (@K_Menu0$[.@j] == "传送服务") {
			// Display Einbroch "没有传送服务" notice.
			if (getarg(1) != 4) {
				callfunc "F_KafTele",getarg(0),"[卡普拉服务人员]";
			} else {
				mes "[卡普拉服务人员]";
				mes "因为根据^FF0000限制传送协定^000000, ";
				mes "卡普拉服务中心无法";
				mes "提供传送服务在秀发兹发德教国中.";
				next;
				mes "[卡普拉服务人员]";
				mes "我们想请您用飞空艇服务替代";
				mes "感谢您的理解和支持.";
			}
			next;
		} else if (@K_Menu0$[.@j] == "手推车租赁服务") {
			if(callfunc("F_KafCart",getarg(0),getarg(4),"[卡普拉服务人员]") == 1)
				next;
		} else if (@K_Menu0$[.@j] == "查询其它信息") {
			callfunc "F_KafInfo",getarg(2);
			next;
		} else if (@K_Menu0$[.@j] == "取消"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		} else if (@K_Menu0$[.@j] == "公会仓库服务") {
			callfunc "F_KafStor",1,0,0,"[卡普拉服务人员]";
			next;
		}
	}
}

//== Storage Function ======================================
// Arguments:
// - 0: Specifies the type of storage that has been requested:
//      0 = Regular storage
//      1 = Guild storage
//      2 = Regular storage, in a castle (free of charge)
// - 1: Cost to use storage.
// - 2: The type of NPC this is being called from. Notable values:
//      0,2-5 = Regular Kafra
//      1 = Niflheim Kafra (special messages)
//      6 = Zonda/Cool Event Staff (no RESRVPTS)
// - 3: NPC title to display.
function	script	F_KafStor	{
	// Unable to access Guild Storage (Busy)
	if (getarg(0) == 1) {
		if (guildopenstorage()) {
			mes getarg(3);
			mes "对不起，公会有另外一位成员正在使用公会仓库";
			mes "现在. 请等待另一位成员用完.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	// Unable to access Normal Storage (Insufficient Basic Skills)
	if (basicskillcheck() && getskilllv(NV_BASIC) < 6 && getskilllv(SU_BASIC_SKILL) == 0) {
		mes getarg(3);
		// Niflheim Specific Message
		if (getarg(2) == 1) {
			mes "^666666S-s-ssoooorry,";
			if (Class == Job_Summoner) {
				mes "您…您需…需要…新的…基本技能";
			} else {
				mes "您…您是一个";
				mes "初心者... 需…需要";
				mes "基本职业技能等级…等级6...^000000";
			}
			return;
		}
		mes "I'm sorry, but you";
		if (Class == Job_Summoner) {
			mes "需要新的基本技能来";
		} else {
			mes "需要基本职业";
			mes "技能等级6才能";
		}
		mes "使用仓库服务.";
		return;
	}
	// Accessing Normal Storage (Skipped if accessing Storage from Guild castle)
	if (getarg(0) != 2) {
		// Consume "卡普拉仓库免费使用券" if available.
		if (countitem(Cargo_Free_Ticket)) {
			delitem Cargo_Free_Ticket, 1;
		} else {
			if (Zeny < getarg(1)) {
				mes getarg(3);
				// Niflheim Specific Message
				if (getarg(2) == 1) {
					percentheal -50,-50;
					mes "^666666zeny...";
					mes "您需要更多的zeny...!";
					mes "需要 150... zeny...";
					mes "呃呃!!^000000";
					return;
				}
				// Standard Message
				mes "对不起,但是您";
				mes "没有足够的 zeny 去使用仓库服务. ";
				mes "仓库使用费用需要 "+getarg(1)+" zeny.";
				return;
			}
			Zeny -= getarg(1);
			if (getarg(2) != 6)
				RESRVPTS += getarg(1) / 5;
		}
	}

	// Surpress the "End" message if in guild castle.
	if (getarg(0) != 2) {
		if (getarg(2) == 1) {
			// Niflheim specific Message
			percentheal 0,-10;
			mes getarg(3);
			for (.@i = 0; .@i < 5; ++.@i)
				mes "^666666感谢您.. 的... 使用...^000000";
		} else if (getarg(2) == 6) {
			// Zonda
			mes getarg(3);
			mes "让我打开您的仓库.";
			mes "感谢您使用卡普拉服务.";
		} else {
			// Normal message
			mes getarg(3);
			mes "在这里，让我打开您的仓库.";
			mes "感谢您使用卡普拉服务.";
		}
	}
	callfunc("F_CheckKafCode"); //check your storage password, if set
	close2;
	openstorage;
	cutin "", 255;
	end;
}

//== Teleport Function =====================================
// Arguments:
// - 1: NPC title to display.
// - 2: The type of NPC this is being called from. Notable values:
//      2 = Guild Castle Kafra (don't consume tickets)
//      6 = Zonda/Cool Event Staff (no RESRVPTS)
function	script	F_KafTele	{
	mes getarg(1);
	mes "请选择";
	mes "您的目的地:";
	next;
	.@menu$ = "";
	for (.@i = 0; .@i <= getarraysize(@wrpD$); ++.@i)
		.@menu$ += @wrpC$[.@i] + ":";
	.@j = select(.@menu$) - 1;
	if (@wrpC$[.@j] == "取消")
		return;
	// Consume "卡普拉传送服务免费使用券" if available.
	// Do not consume if in Guild castle
	if (countitem(Warp_Free_Ticket) > 0 && getarg(0) != 2) {
		delitem Warp_Free_Ticket, 1;
	} else {
		if (Zeny < @wrpP[.@j]) {
			mes getarg(1);
			mes "对不起, 但是您没有";
			mes "足够的 zeny 使用传送服务.";
			mes "传送服务的费用需要";
			mes "to "+@wrpD$[.@j]+" is "+@wrpP[.@j]+" zeny.";
			close2;
			cutin "", 255;
			end;
		}
		Zeny -= @wrpP[.@j];
		if (getarg(0) != 6)
			RESRVPTS += @wrpP[.@j] / 16;
	}
	cutin "", 255;
	if (@wrpD$[.@j] == "艾尔帕兰") warp "aldebaran",168,112;
	else if (@wrpD$[.@j] == "艾尔贝塔") warp "alberta", 117, 56;
	else if (@wrpD$[.@j] == "克魔岛") warp "comodo", 209, 143;
	else if (@wrpD$[.@j] == "发乐斯灯塔岛") warp "cmd_fild07", 127, 134;
	else if (@wrpD$[.@j] == "吉芬") warp "geffen", 120, 39;
	else if (@wrpD$[.@j] == "依斯鲁得") { if (RENEWAL) warp "izlude", 128, 98; else warp "izlude", 91, 105; }
	else if (@wrpD$[.@j] == "朱诺") warp "yuno", 158, 125;
	else if (@wrpD$[.@j] == "妙勒尼山脉") warp "mjolnir_02", 99, 351;
	else if (@wrpD$[.@j] == "梦罗克") warp "morocc", 156, 46;
	else if (@wrpD$[.@j] == "兽人洞穴") warp "gef_fild10", 52, 326;
	else if (@wrpD$[.@j] == "斐扬") warp "payon", 161, 58;
	else if (@wrpD$[.@j] == "普隆德拉") warp "prontera", 116, 72;
	else if (@wrpD$[.@j] == "汶巴拉") warp "umbala", 100, 154;
	else if (@wrpD$[.@j] == "伯仁斯") warp "veins",205,101;
	else if (@wrpD$[.@j] == "拉赫") warp "rachel",115,125;
	end;
}

//== Cart Function =========================================
// Arguments:
// - 0: The type of NPC this is being called from. Notable values:
//      0,1,3-5 = Regular Kafra
//      2 = Guild Castle Kafra (don't consume tickets)
//      6 = Zonda/Cool Event Staff (no RESRVPTS)
// - 1: Cost to rent a cart.
// - 2: NPC title to display.
function	script	F_KafCart	{
	// Ensure that the class wanting to rent a pushcart is a merchant
	if (BaseClass != Job_Merchant) {
		mes getarg(2);
		mes "对不起, 但是";
		mes "手推车租赁服务";
		mes "只开放给商人系列职业.";
		mes "例如银匠, 炼金术士等.";
		mes "和超级初心者的.";
		return 1;
	}
	if (getskilllv(MC_PUSHCART) == 0) {
		mes getarg(2);
		mes "你必须在拥有手推车技能后";
		mes "才能租赁一辆手推车.";
		return 1;
	}
	// Make sure the invoking character does not have a cart already
	if (checkcart() == 1) {
		mes getarg(2);
		mes "您已经拥有了一辆手推车.";
		mes "我们不能在同一时间";
		mes "租赁更多的给顾客.";
		return 1;
	}
	// Consume "卡普拉手推车服务免费使用券" if available.
	// Do not consume if in Guild castle.
	if (countitem(Cart_Free_Ticket) > 0 && getarg(0) != 2) {
		delitem Cart_Free_Ticket, 1;
	} else {
		mes getarg(2);
		mes "手推车租赁的费用需要";
		mes " "+getarg(1)+" zeny. Would";
		mes "您想租一辆手推车吗?";
		next;
		if (select("租一辆手推车", "取消") == 2)
			return 0;
		if (Zeny < getarg(1)) {
			mes getarg(2);
			mes "[卡普拉雇员]";
			mes "对不起, 但是您";
			mes "没有足够的";
			mes "zeny 去支付租赁手推车";
			mes "所需要的 "+getarg(1)+" zeny.";
			return 1;
		}
		Zeny -= getarg(1);
		if (getarg(0) != 6)
			RESRVPTS += 48;
	}
	setcart;
	return 1;
}

//== Special Reserve Points Function =======================
function	script	F_KafInfo	{
	// Uncomment next line to block Kafra Storage Protection
	//.@block = 1;
	setarray .@m$, "查询储蓄点数","仓库密码服务","查看卡普拉位置","取消";
	if (getarg(0) == 2) deletearray .@m$[1],2; // Port Malaya, Rune Knight: remove Storage Password and Kafra Employee Locations
	else if (.@block) deletearray .@m$[1],1; // Remove Storage Password
	while (true) {
		.@j = select(implode(.@m$,":")) - 1;
		if (.@m$[.@j] == "查询储蓄点数") {
			mes "[卡普拉服务人员]";
			mes "让我们看看...";
			mes strcharinfo(PC_NAME) + "...";
			mes "啊, 您总共有 " + RESRVPTS + " 储蓄点数.";
			next;
			mes "[卡普拉服务人员]";
			mes "您可以用这些点数";
			mes "在艾尔帕兰的卡普拉办公室兑换奖励.";
			mes "请多多使用方便的卡普拉服务，顺便多得奖品~";
			next;
			if(getarg(0) == 1)
				return;
		} else if (.@m$[.@j] == "查看卡普拉位置") {
			for (.@i = 0; .@i <= 3; ++.@i)
				viewpoint 1,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
			next;
			for (.@i = 0; .@i <= 3; ++.@i)
				viewpoint 2,@viewpX[.@i],@viewpY[.@i],(.@i+1),0xFF00FF;
		} else if (.@m$[.@j] == "仓库密码服务") {
			callfunc("F_SetKafCode","[卡普拉服务人员]","卡普拉服务");
		} else {
			return;
		}
	}
}

//== End Function ==========================================
// Arguments:
// - 0: used to determine what message to display.
// - 1: used to determine if save message is displayed.
// - 2: used to display the name of the area you're saving in.
function	script	F_KafEnd	{
	mes "[卡普拉服务人员]";
	if (getarg(1) == 1) {
		// Save
		//mes "您的重生地点";
		//mes "现在被保存在这里";
		//mes getarg(2)+".";
		mes "为您储存好了.";
		mes "感谢您使用卡普拉服务.";
	} else if (getarg(0) == 0 || getarg(0) == 5) {
		// Generic End
		mes "我们卡普拉服务中心,";
		mes "一直在努力为您提供最好的服务.";
		mes "我们希望满足您的冒险的需要和优秀的标准.";
	} else if (getarg(0) == 1) {
		// Niflheim End
		percentheal 0,-25;
		mes "^666666卡普拉服务人员";
		mes "一直...直在您的身边~^000000";
	}
	close2;
	cutin "", 255;
	end;
}

//== Check Storage Password Function =======================
function	script	F_CheckKafCode	{
	if (!#kafra_code)
		return;
	mes "请输入您的仓库密码:";
	@code_ = 0;
	input(@code_, 0);
	if (@code_ != #kafra_code) {
		dispbottom "Wrong storage password.";
		close2;
		cutin "",255;
		end;
	}
	@kafcode_try = 0;
	@code_ = 0;
	return;
}

//== Set / Change / Clear Storage Password Function ========
// Arguments:
// - 0: NPC Name
// - 1: Company Name
function	script	F_SetKafCode	{
	mes getarg(0);
	if (#kafra_code) {
		mes "您的仓库现在有密码保护了.您现在还想做什么?";
		next;
		switch (select("修改密码 -> 5000z", "删除仓库密码 -> 1000z", "取消")) {
			case 1:
				mes getarg(0);
				mes "首先, 请您输入 ^0000FF旧密码^000000.";
				@code = callfunc("F_EntKafCode");
				if (!@code || @code != #kafra_code) {
					mes "密码错误. 您无法修改密码，请重新操作.";
					emotion e_hmm;
					break;
				}
				next;
				callsub S_SET,getarg(0),getarg(1);
				break;
			case 2:
				mes getarg(0);
				mes "请输入您的仓库密码.";
				@code = callfunc("F_EntKafCode");
				if (!@code) {
					mes "密码未被删除.";
					emotion e_hmm;
					break;
				}
				next;
				mes getarg(0);
				if (Zeny < 1000) {
					mes "您没有足够的zeny.";
					emotion e_cash;
					break;
				}
				Zeny -= 1000;
				if (@code == #kafra_code) {
					#kafra_code = 0;
					mes "您已成功清除了您的旧密码.";
					mes "感谢您使用 "+getarg(1)+".";
					emotion e_thx;
				} else {
					mes "密码错误. 我们不会归还您的 1000z.";
					mes "请输入正确的密码，再试一次.";
					emotion e_sry;
				}
				break;
			case 3:
				break;
		}
	} else {
		mes getarg(1)+" 自豪地向您介绍一个新的服务:";
		mes "为您的仓库增加密码保护.";
		next;
		switch (select("- 请设定一个新密码 -> 5000z", "取消")) {
			case 1: callsub S_SET,getarg(0),getarg(1); break;
			case 2: break;
		}
	}
	close2;
	cutin "",255;
	end;

S_SET:
	mes getarg(0);
	mes "请输入 ^FF0000新密码^000000 以防止仓库被盗.";
	@code = callfunc("F_EntKafCode");
	if (!@code) {
		mes "密码没有改变.";
		emotion e_hmm;
		return;
	}
	next;
	mes getarg(0);
	if (Zeny < 5000) {
		mes "您没有足够的Zeny.";
		emotion e_cash;
		return;
	}
	Zeny -= 5000;
	#kafra_code = @code;
	mes "您已经拥有了一个密码来保护您的仓库.";
	mes "感谢您的使用 "+getarg(1)+".";
	emotion e_thx;
	return;
}

//== Basic Password Validation Function ====================
function	script	F_EntKafCode	{
	mes "请输入一个1000~10000000之间的数字:";
	@code_ = 0;
	++@kafcode_try;
	if (@kafcode_try > 10) {
		@kafcode_try = 0;
		logmes "破解: 尝试以符合仓库密码.";
	}
	if (input(@code_, 0) == 1) {
		mes "您的密码太长.";
		return 0;
	}
	if (@code_ < 1000) {
		mes "您的密码太短.";
		return 0;
	}
	return @code_;
}

//== Warp Points / View Points Function ====================
function	script	F_KafSet	{
	deletearray @wrpC$;
	deletearray @wrpD$;
	deletearray @wrpP$;
	deletearray @viewpX;
	deletearray @viewpY;
	if (strnpcinfo(NPC_MAP) == "prontera") {
		setarray @wrpD$, "依斯鲁得", "吉芬", "斐扬", "梦罗克", "兽人洞穴", "艾尔贝塔";
		setarray @wrpP, 600, 1200, 1200, 1200, 1200, 1800;
		setarray @viewpX, 151, 29, 282, 152;
		setarray @viewpY, 29, 207, 200, 326;
	} else if (strnpcinfo(NPC_MAP) == "alberta") {
		setarray @wrpP, 1200, 1800, 1800;
		setarray @wrpD$, "斐扬", "梦罗克", "普隆德拉";
		setarray @viewpX, 28, 113, 0, 0;
		setarray @viewpY, 229, 60, 0, 0;
	} else if (strnpcinfo(NPC_MAP) == "aldebaran") {
		setarray @wrpP, 1200, 1200, 1800, 1700;
		setarray @wrpD$, "吉芬", "朱诺" , "依斯鲁得岛", "妙勒尼山脉";
	} else if (strnpcinfo(NPC_MAP) == "comodo" ) {
		setarray @wrpD$, "梦罗克", "发乐斯灯塔岛", "汶巴拉";
		setarray @wrpP, 1800, 1200, 1800;
	} else if (strnpcinfo(NPC_MAP) == "cmd_fild07") {
		setarray @wrpD$, "克魔岛", "梦罗克";
		setarray @wrpP, 1200, 1200;
	} else if (strnpcinfo(NPC_MAP) == "geffen") {
		setarray @wrpD$, "普隆德拉", "艾尔帕兰", "兽人洞穴", "妙勒尼山脉";
		setarray @wrpP, 1200, 1200, 1200, 1700;
		setarray @viewpX, 120, 203;
		setarray @viewpY, 62, 123;
	} else if (strnpcinfo(NPC_MAP) == "izlude") {
		setarray @wrpD$, "吉芬", "斐扬", "梦罗克", "艾尔帕兰";
		setarray @wrpP, 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(NPC_MAP) == "morocc") {
		setarray @wrpD$, "普隆德拉", "斐扬", "艾尔贝塔", "克魔岛", "发乐斯灯塔岛";
		setarray @wrpP, 1200, 1200, 1800, 1800, 1200;
		setarray @viewpX, 156, 163, 28, 292;
		setarray @viewpY, 97, 260, 167, 211;
	} else if (strnpcinfo(NPC_MAP) == "umbala") {
		setarray @wrpD$, "克魔岛";
		setarray @wrpP, 1800;
	} else if (strnpcinfo(NPC_MAP) == "payon") {
		setarray @wrpD$, "普隆德拉", "艾尔贝塔", "梦罗克";
		setarray @wrpP, 1200, 1200, 1200;
	} else if (strnpcinfo(NPC_MAP) == "yuno") {
		setarray @wrpD$, "艾尔帕兰";
		setarray @wrpP, 1200;
		setarray @viewpX, 328, 278, 153, 0;
		setarray @viewpY, 108, 221, 187, 0;
	} else if (strnpcinfo(NPC_MAP) == "job3_rune01") {
		setarray @wrpD$, "依斯鲁得", "吉芬", "斐扬", "梦罗克", "艾尔贝塔";
		setarray @wrpP, 600, 1200, 1200, 1200, 1800;
	} else if (strnpcinfo(NPC_MAP) == "rachel") {
		setarray @wrpD$, "伯仁斯";
		setarray @wrpP, 2200;
	} else if (strnpcinfo(NPC_MAP) == "veins") {
		setarray @wrpD$, "拉赫";
		setarray @wrpP, 2200;
	}
	for (.@i = 0; .@i < getarraysize(@wrpD$); ++.@i)
		@wrpC$[.@i] = @wrpD$[.@i]+" -> "+@wrpP[.@i];
	@wrpC$[getarraysize(@wrpC$)] = "取消";
	return;
}
